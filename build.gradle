import groovy.json.JsonSlurper

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.1"
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'idea'

project.ext.deps = new JsonSlurper().parse(file('dependencies.json')).dependencies

subprojects {
    apply plugin: 'idea'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    version = '0.0.1'
    group = 'io.pity'

    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'


    test {
        beforeTest { desc ->
            println "Executing test ${desc.className}.'${desc.name}'"
        }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: groovydoc) {
        classifier = 'javadoc'
        from groovydoc.destinationDir
    }

    publishing {
        publications {
            standardjava(MavenPublication) {
                from components.java
                artifact sourceJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = bintray_user
        key = bintray_key
        configurations = ['published', 'archives']
        publish = false
        pkg {
            repo = 'maven'
            name = 'pity'
            desc = 'Pity that Bug Report'
            websiteUrl = 'https://github.com/pity/pity.io'
            issueTrackerUrl = 'https://github.com/pity/pity.io/issues'
            vcsUrl = 'https://github.com/pity/pity.io.git'
            licenses = ['Apache-2.0']
            publicDownloadNumbers = true
        }
    }

    project.buildDir = new File(rootDir, 'build/' + project.name)
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
    }
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
    }
}
