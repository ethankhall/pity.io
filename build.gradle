apply plugin: 'dependencies'
apply plugin: 'version'

allprojects {
    apply plugin: 'idea'
    repositories {
        mavenCentral()
        maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
    }

    project.buildDir = new File(rootDir, 'build/' + project.name)

    group = 'io.pity'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: "jacoco"
    apply from: "$rootDir/gradle/bintray.gradle"
    apply from: "$rootDir/gradle/artifacts.gradle"
    apply from: "$rootDir/gradle/publications.gradle"

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    tasks.getByPath('build').dependsOn('jacocoTestReport')

    plugins.withType(GroovyPlugin) {
        compileGroovy {
            groovyOptions.optimizationOptions.all = true
            groovyOptions.configurationScript = file("$rootDir/gradle/compiler-config.groovy")
        }
    }

    test {
        beforeTest { desc ->
            println "Executing test ${desc.className}.'${desc.name}'"
        }
    }

    idea {
        module {
            excludeDirs += buildDir
        }
    }
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.6'
    }
}
