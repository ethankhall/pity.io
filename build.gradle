import groovy.json.JsonSlurper

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.1"
    }
}

apply plugin: 'idea'

project.ext.deps = new JsonSlurper().parse(file('dependencies.json')).dependencies

subprojects {
    apply plugin: 'idea'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'
    apply plugin: 'com.jfrog.bintray'

    version = rootProject.file("VERSION").text
    group = 'io.pity'

    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'


    test {
        beforeTest { desc ->
            println "Executing test ${desc.className}.'${desc.name}'"
        }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: groovydoc) {
        classifier = 'javadoc'
        from groovydoc.destinationDir
    }

    artifacts {
        archives sourceJar
        archives javadocJar
    }

    publishing {
        publications {
            standardjava(MavenPublication) {
                from components.java
                artifact sourceJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = bintray_user
        key = bintray_key
        configurations = ['archives']
        publish = false
        pkg {
            repo = 'maven'
            name = 'pity'
            desc = 'Pity that Bug Report'
            websiteUrl = 'https://github.com/pity/pity.io'
            issueTrackerUrl = 'https://github.com/pity/pity.io/issues'
            vcsUrl = 'https://github.com/pity/pity.io.git'
            licenses = ['Apache-2.0']
            publicDownloadNumbers = true
        }
    }

    project.buildDir = new File(rootDir, 'build/' + project.name)
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
    }
}

task bumpVersion(dependsOn: check) {
    doLast {
        project.exec {
            commandLine 'scripts/version-bump.sh', project.version
        }
    }
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
    }
}
