import groovy.json.JsonSlurper

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.1"
    }
}

allprojects {
    apply plugin: 'idea'
    repositories {
        mavenCentral()
        maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
    }
    project.buildDir = new File(rootDir, 'build/' + project.name)
    
    group = 'io.pity'
    version = rootProject.file("VERSION").text.replaceAll('[^A-Za-z0-9\\.]', '');
}

project.ext.deps = new JsonSlurper().parse(file('dependencies.json')).dependencies

subprojects {
    apply plugin: 'groovy'
    apply from: "$rootDir/gradle/bintray.gradle"
    apply from: "$rootDir/gradle/artifacts.gradle"
    apply from: "$rootDir/gradle/publications.gradle"

    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'

    test {
        beforeTest { desc ->
            println "Executing test ${desc.className}.'${desc.name}'"
        }
    }
}


idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
    }
}
