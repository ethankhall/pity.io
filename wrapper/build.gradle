import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'io.pity.wrapper.WrapperMain'

dependencies {
    compile deps.ivy
    compile deps.'slf4j-api'
    compile deps.logback

    testCompile deps.spock
    testCompile deps.groovy
}

run.dependsOn ':api:publishToMavenLocal', ':bootstrap:publishToMavenLocal', ':tasks:publishToMavenLocal'

processResources {
    filter(ReplaceTokens, tokens: [version: project.version as String])
}

task createExecScript(type: Copy) {
    from file('resources/pity.sh')
    filter(ReplaceTokens, tokens: [version: project.version as String])
    into "$buildDir/dist"
    fileMode 0755
}

artifacts {
    archives shadowJar
    archives file: file("$buildDir/dist/pity.sh"), name: 'pity.sh', builtBy: createExecScript
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifact shadowJar {
                classifier "all"
            }
        }
    }
}
